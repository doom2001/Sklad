

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ЭтаФорма.ЗакрыватьПриВыборе = Ложь;
	
	//Список.Параметры.УстановитьЗначениеПараметра("Склад", Параметры.Склад);
	
	СтрокаОтборПоСкладу = "Остатки на складе: " + СокрЛП(Параметры.Склад);
		
	ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.Использование = Истина;
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Склад");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно; 
	ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ; // Опционально
	ЭлементОтбора.ПравоеЗначение = Параметры.Склад;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПоказатьВводЗначения(Новый ОписаниеОповещения("ПослеВводаКоличества", ЭтотОбъект, Новый Структура("Номенклатура", ВыбраннаяСтрока)),,"Введите количество", "Число");	
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаКоличества(Результат, Параметры) Экспорт	
 
    Если Не Результат = Неопределено Тогда
        
        МассивНайденных = ВыбранныеДанные.НайтиСтроки(Новый Структура("Номенклатура", Параметры.Номенклатура));
	
		Если МассивНайденных.Количество() = 0 Тогда	
			НоваяСтрока = ВыбранныеДанные.Добавить();
			НоваяСтрока.Номенклатура = Параметры.Номенклатура;
			НоваяСтрока.Количество = Результат;
		Иначе
			Строка = МассивНайденных[0];
			Строка.Количество = Строка.Количество + Результат; 		
		КонецЕсли;
        
       		
    КонецЕсли;
 
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	АдресВХранилище = ПеренестиВДокументНаСервере();
	ЭтаФорма.Закрыть(АдресВХранилище);
	
КонецПроцедуры

&НаСервере
Функция ПеренестиВДокументНаСервере()
		
	Возврат ПоместитьВоВременноеХранилище(РеквизитФормыВЗначение("ВыбранныеДанные"), Новый УникальныйИдентификатор());	
	
КонецФункции

